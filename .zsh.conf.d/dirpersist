#!/usr/bin/env zsh
# persitent cd history

DIRSTACKSIZE=${DIRSTACKSIZE:-20}
dirstack_file=${dirstack_file:-${HOME}/.zdirs}

if [[ -f ${dirstack_file} ]] && [[ ${#dirstack[*]} -eq 0 ]] ; then
  dirstack=( ${(f)"$(< $dirstack_file)"} )
  # "cd -" won't work after login by just setting $OLDPWD, so
  [[ -d $dirstack[1] ]] && cd $dirstack[1] && cd $OLDPWD
fi

chpwd-dirpersist() {
  if (( $DIRSTACKSIZE <= 0 )) || [[ -z $dirstack_file ]]; then return; fi
  local -ax my_stack
  my_stack=( ${PWD} ${dirstack} )
  builtin print -l ${(u)my_stack} >! ${dirstack_file}
}

fzf-chpwd-hist() {
  local selected=$(dirs -v | fzf | cut -d$'\t' -f2)
  if [[ -n "${selected}" ]]; then
      BUFFER="cd ${selected}"
      zle accept-line
  fi
  zle reset-prompt
}
zle -N fzf-chpwd-hist

autoload -U add-zsh-hook
add-zsh-hook chpwd chpwd-dirpersist
zle -N fzf-chpwd-hist
bindkey '^ ' fzf-chpwd-hist
